plugins {
    id 'com.enonic.defaults' version '2.1.2'
    id 'com.enonic.xp.app' version '3.2.0'
    id 'com.enonic.xp.doc' version '2.0.0'
    id 'com.github.node-gradle.node' version '5.0.0'
    id 'maven-publish'
    id 'jacoco'
}

apply from: "$rootDir/gradle/env.gradle"
apply from: "$rootDir/gradle/node.gradle"

app {
    name = project.appName
    displayName = 'Guillotine'
    vendorName = 'Enonic AS'
    vendorUrl = 'https://enonic.com'
    systemVersion = "${xpVersion}"
}

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
}

compileTestJava {
    options.encoding = defaultEncoding
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:script-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"

    include "com.graphql-java:graphql-java:20.3"
    include "com.graphql-java:graphql-java-extended-scalars:20.2"

    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include "com.enonic.lib:lib-mustache:2.1.1"
    include "com.enonic.lib:lib-static:1.0.3"
    include "com.enonic.lib:lib-router:3.0.1"

    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation "com.enonic.xp:testing:${xpVersion}"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo( 'dev' )
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}

node {
    version = '16.0.0'
    download = true
}

task lint( type: NpmTask, group: "verification", dependsOn: npmInstall ) {
    args = ['run', 'lint:quiet']
    inputs.files fileTree( dir: 'src', include: '**/*.ts' )
}

check.dependsOn( lint )

task webpack( type: NpmTask, dependsOn: npmInstall ) {
    environment = ['NODE_ENV': nodeEnvironment()]
    description = 'Build UI resources (ts, css, etc).'
    args = ['run', 'build']
    inputs.dir "src/main"
    inputs.file 'webpack.config.js'
    outputs.dir "$buildDir/resources/main"
}

jar {
    exclude 'assets/**/*.tsx'
    exclude 'assets/**/*.less'
    exclude 'assets/styles/graphiql.min.css'
    exclude 'assets/**/styles/*.js'

    if ( isProd() )
    {
        exclude 'assets/**/*.map'
    }

    outputs.dir "$buildDir/resources/main"

    dependsOn webpack
}
