plugins {
    id 'com.enonic.defaults' version '2.1.5'
    id 'com.enonic.xp.app' version '3.6.1'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'maven-publish'
    id 'jacoco'
}

apply from: "$rootDir/gradle/env.gradle"
apply from: "$rootDir/gradle/node.gradle"

app {
    name = project.appName
    displayName = 'Guillotine'
    vendorName = 'Enonic AS'
    vendorUrl = 'https://enonic.com'
    systemVersion = "${xpVersion}"
}

def defaultEncoding = 'UTF-8'

compileJava {
	options.encoding = defaultEncoding
}

compileTestJava {
	options.encoding = defaultEncoding
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = sourceCompatibility

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:script-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.19.0"

    include "com.graphql-java:graphql-java:24.0"
    include "com.graphql-java:graphql-java-extended-scalars:22.0"

    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include "com.enonic.xp:lib-app:${xpVersion}"
    include "com.enonic.lib:lib-mustache:2.1.1"
    include "com.enonic.lib:lib-static:2.1.1-SNAPSHOT"
    include "com.enonic.lib:lib-asset:1.0.3-SNAPSHOT"
    include "com.enonic.lib:lib-router:3.1.0"

    testImplementation(platform("org.junit:junit-bom:5.12.2"))
    testImplementation(platform("org.mockito:mockito-bom:5.18.0"))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation "com.enonic.xp:testing:${xpVersion}"
}

repositories {
    mavenCentral()
    xp.enonicRepo( 'dev' )
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}

node {
    version = '18.17.1'
    download = true
}

tasks.register('lint', NpmTask) {
    group 'verification'
    dependsOn( npmInstall )
    args = ['run', 'lint:quiet']
    inputs.files fileTree( dir: 'src', include: '**/*.ts' )
}

check.dependsOn( lint )

tasks.register('webpack', NpmTask) {
    dependsOn( npmInstall )
    environment = ['NODE_ENV': nodeEnvironment()]
    description = 'Build UI resources (ts, css, etc).'
    args = ['run', 'build']
    inputs.dir "src/main"
    inputs.file 'webpack.config.js'
    outputs.dir "$buildDir/resources/main"
}

jar {
    exclude 'assets/**/*.tsx'
    exclude 'assets/**/*.less'
    exclude 'assets/styles/graphiql.min.css'
    exclude 'assets/**/styles/*.js'

    if ( isProd() )
    {
        exclude 'assets/**/*.map'
    }

    outputs.dir "$buildDir/resources/main"

    dependsOn( webpack )
}

javadoc.dependsOn( webpack, jar )
compileTestJava.dependsOn( webpack, jar )
